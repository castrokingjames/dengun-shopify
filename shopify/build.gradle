import com.dengun.shopify.*

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android-extensions"

android {
    compileSdkVersion Config.compile

    defaultConfig {
        applicationId = Config.applicationId
        minSdkVersion Config.min
        targetSdkVersion Config.target
        versionCode = Config.versionCode
        versionName = Config.versionName

    }

    buildTypes {
        Properties properties = new Properties()
        properties.load(new FileInputStream("local.properties"))
        def shopifyApiKey = properties.getProperty("SHOPIFY_API_KEY")
        def shopiyBaseURL = "https://shopicruit.myshopify.com"

        debug {
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            buildConfigField "String", "SHOPIFY_API_KEY", "\""+shopifyApiKey+"\""
            buildConfigField "String", "SHOPIFY_BASE_URL", "\""+shopiyBaseURL+"\""
        }

        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            buildConfigField "String", "SHOPIFY_API_KEY", "\""+shopifyApiKey+"\""
            buildConfigField "String", "SHOPIFY_BASE_URL", "\""+shopiyBaseURL+"\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        pickFirst 'META-INF/kotlinx-io.kotlin_module'
        pickFirst 'META-INF/atomicfu.kotlin_module'
        pickFirst 'META-INF/kotlinx-coroutines-io.kotlin_module'
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
        test.java.srcDirs += "src/test/kotlin"
        androidTest.java.srcDirs += "src/androidTest/kotlin"
    }
}

dependencies {
    implementation project(":di-scope")
    implementation project(":di-module")
    implementation project(":ui-navigation")
    implementation project(":ui-mvrx")
    implementation project(":ui-common")
    implementation project(":domain")
    implementation project(":data")
    implementation project(":data-local")
    implementation project(":data-remote")
    implementation project(":manager")
    implementation project(":manager-android-dispatcher")
    implementation project(":feature-tag-list")
    implementation project(":feature-product-list")
    implementation project(":timber")

    implementation Libs.AndroidX.Navigation.fragment
    implementation Libs.AndroidX.Navigation.ui

    implementation Libs.Airbnb.mvRx

    implementation Libs.AndroidX.appcompat

    implementation Libs.Dagger.dagger
    implementation Libs.Dagger.android
    implementation Libs.Dagger.androidSupport
    implementation Libs.AssistedInject.annotationDagger2

    kapt Libs.Dagger.compiler
    kapt Libs.Dagger.androidProcessor
    kapt Libs.AssistedInject.processorDagger2

    implementation Libs.Facebook.stetho
}